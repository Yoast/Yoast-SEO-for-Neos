prototype(Yoast.YoastSeoForNeos:Component.Application) < prototype(Neos.Fusion:Tag) {
    attributes {
        class = 'yoast-seo__application'
        id = 'yoast-app'
        data-configuration = Neos.Fusion:RawArray {
            title = ${q(documentNode).property('title')}
            titleOverride = ${q(documentNode).property('titleOverride')}
            description = ${q(documentNode).property('metaDescription')}
            focusKeyword = ${q(documentNode).property('focusKeyword')}
            isCornerstone = ${q(documentNode).property('isCornerstone')}
            uriPathSegment = ${q(documentNode).property('uriPathSegment')}
            isHomepage = ${site == documentNode}
            isAmp = false
            uiLocale = Yoast.YoastSeoForNeos:InterfaceLanguage

            translationsUrl = '/neosyoastseo/data/fetchTranslations'
            workerUrl = Neos.Fusion:ResourceUri {
                path = 'resource://Yoast.YoastSeoForNeos/Public/Assets/webWorker.js'
            }
            previewUrl = ${'/neosyoastseo/page/renderPreviewPage?node=' + documentNode.contextPath}
            baseUrl = Yoast.YoastSeoForNeos:BaseUri
            siteUrl = Neos.Neos:NodeUri {
                node = ${q(site).context({workspaceName: 'live'}).get(0)}
                absolute = true
            }
            pageUrl = Neos.Neos:NodeUri {
                node = ${q(documentNode).context({workspaceName: 'live'}).get(0)}
                absolute = true
            }
            faviconSrc = ${this.siteUrl + '/favicon.ico'}

            breadcrumbs = Neos.Fusion:RawCollection {
                collection = ${Array.reverse(q(documentNode).parents('[instanceof Neos.Neos:Document]').get())}
                itemRenderer = ${q(item).property('uriPathSegment')}
            }

            contentSelector = ${Configuration.setting('Neos.Neos.Ui.frontendConfiguration')['Yoast.YoastSeoForNeos']['contentSelector']}

            @process.encode = ${Json.stringify(value)}
        }
    }
}
