prototype(Yoast.YoastSeoForNeos:Component.Application) < prototype(Neos.Fusion:Tag) {
    attributes {
        class = 'yoast-seo__application'
        id = 'yoast-app'
        data-configuration = Neos.Fusion:DataStructure {
            title = ${q(documentNode).property('title')}
            titleOverride = ${q(documentNode).property('titleOverride')}
            description = ${q(documentNode).property('metaDescription')}
            focusKeyword = ${q(documentNode).property('focusKeyword')}
            isCornerstone = ${!!q(documentNode).property('isCornerstone')}
            uriPathSegment = ${q(documentNode).property('uriPathSegment')}
            isHomepage = ${site == documentNode}
            isAmp = false
            uiLocale = Yoast.YoastSeoForNeos:InterfaceLanguage

            translationsUrl = '/neosyoastseo/data/fetchTranslations'
            workerUrl = Neos.Fusion:ResourceUri {
                path = 'resource://Yoast.YoastSeoForNeos/Public/Assets/webWorker.js'
            }
            previewUrl = ${'/neosyoastseo/page/renderPreviewPage?node=' + String.rawUrlEncode(Neos.Node.serializedNodeAddress(documentNode))}
            baseUrl = Yoast.YoastSeoForNeos:BaseUri
            siteUrl = Neos.Neos:NodeUri {
                node = ${q(site).context({workspaceName: 'live'}).get(0)}
                absolute = true
            }
            pageUrl = Neos.Neos:NodeUri {
                node = ${q(documentNode).context({workspaceName: 'live'}).get(0)}
                absolute = true
            }
            // Default url to check for the favicon if not set as link tag in the html head with `rel="shortcut icon"` or `rel="icon"`.
            faviconSrc = ${this.siteUrl + '/favicon.ico'}

            breadcrumbs = Neos.Neos:NodeUri {
                node = ${q(documentNode).context({workspaceName: 'live'}).get(0)}
                absolute = false
                @process.split = ${Array.slice(Array.values(Array.filter(String.split(value, '/'))), 0, -1)}
            }

            twitterFallbackImage = Neos.Fusion:ResourceUri {
                path = 'resource://Yoast.YoastSeoForNeos/Public/Assets/twitter.svg'
            }
            openGraphFallbackImage = Neos.Fusion:ResourceUri {
                path = 'resource://Yoast.YoastSeoForNeos/Public/Assets/facebook.svg'
            }

            contentSelector = ${Configuration.setting('Neos.Neos.Ui.frontendConfiguration')['Yoast.YoastSeoForNeos']['contentSelector']}

            @process.encode = ${Json.stringify(value)}
        }
    }
}
