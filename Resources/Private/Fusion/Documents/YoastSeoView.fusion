prototype(Yoast.YoastSeoForNeos:Documents.YoastSeoView) < prototype(Neos.Neos:Page) {

    @context.interfaceLanguage = Yoast.YoastSeoForNeos:InterfaceLanguage

    head {
        stylesheets >
        stylesheets = Neos.Fusion:Join {
            main = afx`
                <link rel="stylesheet" type="text/css">
                    <Neos.Fusion:ResourceUri @path="attributes.href" path="resource://Yoast.YoastSeoForNeos/Public/Assets/main.css"/>
                </link>
            `
        }
    }

    bodyTag.attributes.class = 'neos-yoast-seo-view-mode'

    body >
    body = Neos.Fusion:Component {
        @position = 'after bodyTag'
        node = ${node}
        site = ${site}

        renderer = Neos.Fusion:Join {
            appWrapper = afx`
                <main class="yoast-seo__container" @key="appWrapper">
                    <Yoast.YoastSeoForNeos:Component.Header/>
                    <Yoast.YoastSeoForNeos:Component.Application/>
                    <Yoast.YoastSeoForNeos:Component.Modal/>
                    <Yoast.YoastSeoForNeos:Component.SnippetEditor/>
                </main>
                <script @key="yoastBundle">
                    <Neos.Fusion:ResourceUri @path="attributes.src" path="resource://Yoast.YoastSeoForNeos/Public/Assets/yoastseo.js"/>
                </script>
                <script async="async" @key="appBundle">
                    <Neos.Fusion:ResourceUri @path="attributes.src" path="resource://Yoast.YoastSeoForNeos/Public/Assets/app.js"/>
                </script>
            `
        }
    }
}
